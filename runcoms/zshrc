_source_if_exists() {
  [[ -f "$1" ]] && source "$1"
}

# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
_source_if_exists ~/.p10k.zsh

if [[ -s "${ZDOTDIR:-$HOME}/.zprezto/init.zsh" ]]; then
  source "${ZDOTDIR:-$HOME}/.zprezto/init.zsh"
fi


_fzf_compgen_path() {
  fd --hidden --follow --exclude ".git" . "$1"
}

_fzf_compgen_dir() {
  fd --type d --hidden --follow --exclude ".git" . "$1"
}

_fzf_complete_pass() {
  local store="$HOME/.password-store"
  _fzf_complete --multi --reverse --prompt="pass> " -- "$@" < <(
    fd -e gpg -t file -c never . "$store" | sed "s|$store/\(.*\)\.gpg$|\\1|"
  )
}

FZF_PREVIEW_MAX_LINES=100
FZF_PREVIEW_DIR_OPTS="tree -L 3 -C {} 2>/dev/null | head -$FZF_PREVIEW_MAX_LINES"
FZF_PREVIEW_FILE_OPTS="bat -p --color=always --line-range :$FZF_PREVIEW_MAX_LINES {} 2> /dev/null"
FZF_PREVIEW_OPTS="($FZF_PREVIEW_FILE_OPTS || $FZF_PREVIEW_DIR_OPTS)"

export FZF_CTRL_T_OPTS="--preview '$FZF_PREVIEW_OPTS' --bind 'ctrl-p:toggle-preview' --preview-window hidden --header ':: C-p (toggle preview)'"
export FZF_ALT_C_OPTS="--preview '$FZF_PREVIEW_DIR_OPTS' --bind 'ctrl-p:toggle-preview' --preview-window hidden --header ':: C-p (toggle preview)'"

_source_if_exists /usr/share/fzf/key-bindings.zsh
_source_if_exists /usr/share/fzf/completion.zsh
_source_if_exists /usr/share/doc/fzf/examples/key-bindings.zsh

# functions
refresh-env() {
  local socket_path="$(tmux show-environment | sed -n 's/^SSH_AUTH_SOCK=//p')"

  if ! [[ "$socket_path" ]]; then
    echo 'no socket path' >&2
    return 1
  else
    export SSH_AUTH_SOCK="$socket_path"
  fi
}

_do_fs() {
  if [ "$#" -ne 1 ]; then echo "Need a string to search for!"; return 1; fi
  local query files
  query=$1
  files="$(rg --max-count=1 --smart-case --files-with-matches --no-messages $_DO_FS_EXTRA $query | fzf -m --exit-0 --preview="rg --smart-case --pretty --context 20 '"$query"' {}")"
  if [ $? -eq 0 -a -n "$files" ]; then
    files=$(sed 's/^\(.*\)$/"\1"/' <<< $files)
    [[ -n $files ]] && xargs "$VISUAL" <<< $files
  fi
}

fsa() {
  _DO_FS_EXTRA="-uuu" _do_fs "$@"
}

fs() {
  _do_fs "$@"
}

_do_ff() {
  local out key files cmd
  IFS=$'\n' \
    out="$(fd --type f $_DO_FF_EXTRA "$*" | fzf -m --exit-0 --preview=$FZF_PREVIEW_OPTS --bind 'ctrl-p:toggle-preview' --preview-window hidden --expect=ctrl-o --header ":: CR (edit) C-p (toggle preview) C-o (open)")"
  if [ $? -eq 0 ]; then
    key=$(head -1 <<< "$out")
    files=$(tail +2 <<< "$out")
    [[ -z "$files" ]] && return 0
    files=$(sed 's/^\(.*\)$/"\1"/' <<< $files)
    cmd="$VISUAL"
    if [[ "$key" == "ctrl-o" ]]; then
      cmd="xdg-open"
    fi
    xargs "$cmd" <<< $files
  fi
}

ffa() {
  _DO_FF_EXTRA="-HI" _do_ff "$*"
}

ff() {
  _DO_FF_EXTRA="" _do_ff "$*"
}

# cd into given dir or search in recently used dirs
function j() {
  local dir
  dir="$(fasd -Rdl | rg -i "$*")"
  dir_size=$(echo "$dir" | wc -l)
  if [ $dir_size -eq 1 ]; then
    cd "${dir}"
  elif [ $dir_size -gt 1 ]; then
    dir=$(echo -e "$dir" | fzf --ansi -0 --no-sort --no-multi --query "$*" --preview $FZF_PREVIEW_DIR_OPTS --bind 'ctrl-p:toggle-preview' --preview-window hidden --header ":: C-p (toggle preview)") && cd "${dir}" || return 1
  else
    return 0
  fi
}

if command -v fzf &>/dev/null; then
  eval "$(fzf --zsh)"
fi

alias config='git --git-dir=$HOME/.dotfiles/ --work-tree=$HOME'
alias glg='git log --all --decorate --graph --oneline'
alias mergetool='vim -c DiffviewOpen'

HISTSIZE=50000
SAVEHIST=50000

if command -v nvim &>/dev/null; then
  alias vim=nvim
  alias vi=nvim
  export EDITOR=nvim
  export VISUAL=nvim
fi

alias e=$EDITOR

# bind C-BS
bindkey '^[[127;5u' backward-kill-word

# lazygit
if command -v lazygit &>/dev/null; then
  alias lg='git lg'
fi

# FASD
_FASD_RESOLVE_SYMLINKS=1
unalias j

# ghcup
_source_if_exists "/home/krikava/.ghcup/env"

# rust
[[ -d "$HOME/.cargo/bin" ]] && export PATH="$PATH:$HOME/.cargo/bin"
_source_if_exists "$HOME/.cargo/env"

function setup_osx() {
  if [[ -f /opt/homebrew/bin/brew ]]; then
    eval "$(/opt/homebrew/bin/brew shellenv)"
  fi

  # that is why we want to have it early, before the prezto
  fpath=($(brew --prefix)/share/zsh/site-functions ${fpath})

  _source_if_exists $(brew --prefix asdf)/libexec/asdf.sh
}

function setup_linux() {
  alias pbcopy='xclip -sel c'
  alias pcpaste='xclip -o'

  if command -v asdf > /dev/null 2>&1; then
    ASDF_DIR="/opt/asdf-vm"
    # setup some additional completions
    [[ -d "$ASDF_DIR" ]] && fpath=("$ASDF_DIR/completions" $fpath)
    _source_if_exists "$ASDF_DIR/asdf.sh"
  fi
}

case $( uname -s ) in
  Linux)
    setup_linux
    ;;
  Darwin)
    setup_osx
    ;;
esac

